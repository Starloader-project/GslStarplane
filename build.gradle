plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group = 'de.geolykt.starloader'
archivesBaseName = 'gsl-starplane'
def baseVersion = '0.1.0'

targetCompatibility = compileJava.targetCompatibility = '11' // VF is Java 11+ - so we have little other choices for the time being
sourceCompatibility = compileJava.sourceCompatibility = '1.8' // Just in case we need to go back to Java 8 in the future

repositories {
    mavenLocal()
    maven {
        name 'fabric'
        url 'https://maven.fabricmc.net/'
    }
    maven {
        name 'stianloader-maven'
        url 'https://stianloader.org/maven/'
    }
    mavenCentral()
}

dependencies {
    implementation "de.geolykt.starloader:sl-deobf:0.1.1-a20231127"
    implementation "de.geolykt.starloader:reversible-access-setter:1.0.0"
    implementation gradleApi()
    implementation "org.ow2.asm:asm:9.6"
    implementation "org.ow2.asm:asm-tree:9.6"
    implementation "org.ow2.asm:asm-util:9.6"
    implementation "org.ow2.asm:asm-commons:9.6"

    // https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20240205'

    // https://mvnrepository.com/artifact/net.fabricmc/tiny-remapper
    implementation 'net.fabricmc:tiny-remapper:0.10.1'
    // https://mvnrepository.com/artifact/org.vineflower/vineflower
    implementation 'org.vineflower:vineflower:1.9.3'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'
}

gradlePlugin {
    // Define the plugin
    plugins {
        gslStarplanePlugin {
            id = project.archivesBaseName
            version = baseVersion
            implementationClass = 'de.geolykt.starloader.gslstarplane.GslStarplanePlugin'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version baseVersion

            from components['java']

            //artifact sourcesJar
            //artifact javadocJar
        }
    }
    repositories {
        if (System.getProperty('publishRepo') != null) {
            maven {
                url System.getProperty('publishRepo')
                allowInsecureProtocol = true
            }
        } else {
            mavenLocal()
        }
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group = 'de.geolykt.starloader'
archivesBaseName = 'gcmc-starplane'
def baseVersion = '0.1.0'

targetCompatibility = compileJava.targetCompatibility = '11' // QF is Java 11+ - so we have little other choices for the time being
sourceCompatibility = compileJava.sourceCompatibility = '1.8' // Just in case we need to go back to Java 8 in the future

repositories {
    mavenLocal()
    maven {
        name 'fabric'
        url 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

dependencies {
    implementation "de.geolykt.starloader:sl-deobf:0.1.1"
    implementation "de.geolykt.starloader:reversible-access-setter:1.0.0"
    implementation gradleApi()
    implementation "org.ow2.asm:asm:9.5"
    implementation "org.ow2.asm:asm-tree:9.5"
    implementation "org.ow2.asm:asm-util:9.5"
    implementation "org.ow2.asm:asm-commons:9.5"

    // https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20230227'

    // https://mvnrepository.com/artifact/net.fabricmc/tiny-remapper
    implementation 'net.fabricmc:tiny-remapper:0.8.6+local'
    // https://mvnrepository.com/artifact/org.quiltmc/quiltflower
    implementation 'org.quiltmc:quiltflower:1.9.0'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.0.1'
}

gradlePlugin {
    // Define the plugin
    plugins {
        gslStarplanePlugin {
            id = project.archivesBaseName
            version = baseVersion
            implementationClass = 'de.geolykt.starloader.gcmcstarplane.GcmcStarplanePlugin'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version baseVersion

            from components['java']

            //artifact sourcesJar
            //artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
        /*maven {
            url = "/home/Geolykt/maven-alt/repo/"
        }*/
    }
}
